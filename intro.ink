-> intro

// the idea here is that Amber enters the ghost world and talks to the kiosk ghost. at this point, i could imagine letting the player ask her more questions, but this might work for now.
// there are no variables or anything in this file and it should not have to rely on any other file for it to work. at the end, it transitions to the lobby area, and the player can choose where they want Amber to go
=== intro ===
Someone new comes into focus: someone who doesn't belong here, someone warm, beating, bloody, someone human. She must be lost.

<em>Ding.</em>

// maybe this could use more description of the space? or maybe that should wait until later?
The elevator doors open and Amber walks out. She looks around: something is off. The lobby is a bit dimmer than she remembers. In fact, the light bulbs in the ceiling seem to be flickering. And they're blue. Were they always blue? 
The guy who normally works at the kiosk isn't there. The woman standing there now must work the night shift. Amanda approaches her. Did they put the AC on? She checks her phone: 1:00 AM.
"Hi," Amber says to the woman at the kiosk. 
She just stares back.

* [Ask her what is going on.] "What is happening here? Why is it so dark?"

* [Ask her about the night shift.] "Do you only work the night shift?"

* [Ask her about the AC.] "Can you turn the AC down? It's freezing in here."
"The air conditioning isn't on," the woman said. <>

- -> debbie_conversation

=== debbie_conversation ===
"You don't belong here."

Amanda just looks at her. "What, do you think I'm twelve?"

"That's not what I mean." The woman looks distant, frightened almost. She offers Amber her hand, palm up. Amber isn't sure what to do with it.

* [Shake it.] Amber goes in for a hand shake, not knowing exactly what to do with her fingers. She never felt quite comfortable shaking people's hands, but it felt proper in the situation.
She can't grip the woman's hand, and instead goes right through it.

* [Give her a high-five.] Amber goes in fast for a high-five. She feels like she's playing a game with her.
She goes right through her hand, hitting the black granite counter top.

* [Fist-bump her.] Amber goes for a fist-bump. That's what she's used to. She's hoping it will diffuse the situation.
She akwardly jabs her fist forward and she goes right through her hand. Amber recoiled at the sight of her fist inside of the woman's wrist.

-
The woman looks apolagetic. "Do you see why you don't belong here?"
Panic fills Amber's chest. She doesn't know what to think. 
"Am I dead?" she blurts. She puts her hand to her heart, then her neck, and then her fingers to her wrist. She doesn't know what to believe. 
"No. You've entered the world of the dead, through that elevator there. We keep meaning to have that extra button removed, but maintenance hasn't gotten around to it yet."
Of course. The skull button. Amber had sneaked out of her and her sister's hotel room because she couldn't sleep. She wanted to go on a walk outside, but she was distracted by a mysterious skull button on the elevator.
She put her palms to her face. "I'm such an idiot. For real."
"Don't fret! There is a ritual that you can do to get back to where you belong. You'll just need to find a few things."

// maybe this is a good moment for more description?
Amber tries to keep calm. She looks behind her, taking in the lobby. The fountain, in the center of the room, gurgles. In the corners of the wide room are a gift shop, a seating area for a restaraunt, a lounge, and another room Amber can't identify.

* [Stay calm.]
Amber doesn't know how to stay calm.

* [Panic.]

-
Her breathing quickens as she looks around. She makes eye contact with a few strangers, all of whom must be dead. Did they know she was alive?
"Hey," the ghost says. "Just breathe, that's what the living does, right?"
Amber breathes. She smiles, processing the joke. "Yeah, that's generally what we like to do."
"What's your name?"
"Amber."
"I'm Debbie." After a pause, she goes on. "I think you're gonna be alright. Do you want to hear about the ritual?"
"Yeah."
"So you need to find five candles, some charcoal, and a precious stone, okay? You should be able to find them around here."

-> ask_about_ritual_items

=== ask_about_ritual_items ===

* [Ask about the candles.] "Where would I find candles?"
"There should be a man by the name of Burns who has some candles. He's been staying here a while, trying to sell those candles, but no one seems interested. He's staying in room 203"
"That's the room my parents are staying in."
"Great, so you know where it is!"
Amber wonders what she will find in the ghost version of 203. Will her parents be there, sleeping? Will their stuff be there? Would Ruby be asleep in the room next door?

* [Ask about the charcoal.]
"You should be able to find some in the kitchen. The chef there can be kind of a stickler, but if you get on his good side, he will probably give you some. Feel free to knock on the kitchen door."
Amber wonders what "stickler" means in this context. What will it take to get on his good side? 

* [Ask about the precious stone.]
"There should be an elegent-looking woman around here somewhere with way too much jewelry for her own good. I bet you can get her to part with some of it if you're lucky!"
Amber wonders how lucky she will have to be. She doesn't like the idea of having to beg some rich woman to give up her jewelry.

* [Thank Debbie for helping out.]
"Thank you!"
"Of course! Please ask me if you need any more help>"
Amber nods and turns around. The lobby seems impossibly huge.
-> end_of_scene

* [Start searching for the stuff Debbie mentioned.]
Amber turns around before thinking <em>no, I should thank her first.</em> She thanks her, feeling slightly embarassed, and takes a few steps away. The lobby seems impossibly huge.
-> end_of_scene

- -> ask_about_ritual_items
-> END

== end_of_scene ==
-> END
// so here we would divert into the rest of the story





